/**
* Author: wladimiiir
*/

#include "_macros.fos"
#include "serializator.fos"
#include "npc_spawn_h.fos"
#include "wrappers.fos"
#include "battleground_h.fos"
#include "vr_custom_locations.fos"

#define	SIMULATION_TYPE_HUNT				( 0 )
#define	SIMULATION_TYPE_TEAM_DEATHMATCH		( 1 )
#define	SIMULATION_TYPE_CUSTOM_LOCATION		( 2 )

#include "virtual_reality_dialog.fos"

import Item@ FindHiddenContainer(Map& map) from "stdlib";
import bool UpdateItems(Critter& npc) from "trader";
import Battle@ AddHunt(Critter& creator, uint combatMode) from "battleground";
import Battle@ AddTeamDeathmatch(Critter& creator, uint combatMode) from "battleground";
import Battle@ AddCustomLocation(Critter& creator, uint combatMode, string name, bool multiLocation) from "battleground";

/**
* Init functions
*/
void InitAdmin(Critter& critter, bool firstTime)
{
	if(firstTime)
	{
		critter.StatBase[ST_DIALOG_ID] = DIALOG_vr_administrator;
		critter.ModeBase[MODE_NO_BARTER] = 0;
	}
		
	critter.SetEvent(CRITTER_EVENT_BARTER, "_VRAdministratorBarter");
}

bool InitTerminal(Critter& critter, Scenery& scenery, int skill, Item@ item)
{
	if(skill != SKILL_PICK_ON_GROUND)
		return false;
	
	RunDialog(critter, DIALOG_vr_simulation, scenery.HexX, scenery.HexY, true);
	return true;
}

bool _VRAdministratorBarter(Critter& npc, Critter& player, bool attach, uint barterCount)
{
	#define	REAL_WORLD_ITEMS_PREFIX				("RW_")
	#define	VIRTUAL_REALITY_ITEMS_PREFIX		("VR_")
	#define	HAND1_PREFIX						("HAND1_")
	#define	HAND2_PREFIX						("HAND2_")
	#define	ARMOR_PREFIX						("ARMOR_")
	#define	INV_PREFIX							("INV_")
	
	Item@ hiddenContainer = FindHiddenContainer(npc.GetMap());
	ItemHolderWrapper itemHolder;
	
	if(valid(hiddenContainer))
		itemHolder = ItemHolderWrapper(hiddenContainer);
	else
		itemHolder = ItemHolderWrapper(npc);
	
	if(attach)
	{
		//adding items to admin
		UpdateItems(npc); 
		
		//holding current real world items
		Item@[] items;
		player.GetItems(SLOT_INV, items);
		itemHolder.PutItems(REAL_WORLD_ITEMS_PREFIX + player.Id, items);
		
		//adding digital credit
		if(player.Param[DIGITAL_CREDIT] > 0)
			player.AddItem(PID_DIGITAL_CREDIT, player.Param[DIGITAL_CREDIT]);
		
		//getting his virtual reality items
		items = itemHolder.PopItems(VIRTUAL_REALITY_ITEMS_PREFIX + player.Id);
		MoveItems(items, player);
	}
	else
	{
		//getting VR items
		Item@[] items;
		player.GetItems(SLOT_INV, items);
		
		for (uint i = 0; i < items.length(); i++)
		{
			Item@ item = items[i];
			//setting current digital credit
			if(item.GetProtoId() == PID_DIGITAL_CREDIT)
			{
				player.ParamBase[DIGITAL_CREDIT] = item.GetCount();
				items.removeAt(i);
				DeleteItem(item);
				break;
			}
		}
		
		//switching VR items with real world items
		itemHolder.PutItems(VIRTUAL_REALITY_ITEMS_PREFIX + player.Id, items);
		items = itemHolder.PopItems(REAL_WORLD_ITEMS_PREFIX + player.Id);
		MoveItems(items, player);
	}
	
	return true;
}


/**
* Entities
*/
class VRSimulation
{
	#define _GetKey	("VRSimulation_" + _Critter.Id)
	
	private Critter@ _Critter;
	private uint Type;
	private uint LocationPid;
	private uint Duration;
	private uint CombatMode;
	private uint Spawn;
	private VRCustomLocation@ CustomLocation;
	
	VRSimulation(Critter& critter)
	{
		@_Critter = critter;
		Type = SIMULATION_TYPE_HUNT;
		LocationPid = LOCATION_GreenForest2;
		Duration = 0;
		CombatMode = COMBAT_MODE_TURN_BASED;
		Spawn = NPC_SPAWN_VR_GECKOS;
		@CustomLocation = CustomLocations.isEmpty() ? null : CustomLocations[0];
		Load();
	}
	
	private void Load()
	{
		Serializator serializator;
		if(!serializator.Load(_GetKey))
			return;
		
		serializator.Get(Type);
		serializator.Get(LocationPid);
		serializator.Get(Duration);
		serializator.Get(CombatMode);
		serializator.Get(Spawn);
		
		string customLocationId;
		serializator.Get(customLocationId);
		if(valid(customLocationId))
			@CustomLocation = FindCustomLocation(customLocationId);
	}
	
	private void Save()
	{
		Serializator serializator;
		serializator.Set(Type);
		serializator.Set(LocationPid);
		serializator.Set(Duration);
		serializator.Set(CombatMode);
		serializator.Set(Spawn);
		serializator.Set(valid(CustomLocation) ? CustomLocation.Id : "");
		serializator.Save(_GetKey);
	}
	
	void SetType(uint simulationType)
	{
		Type = simulationType;
		Save();
	}
	
	void SetLocation(uint locationPid)
	{
		LocationPid = locationPid;
		Save();
	}
	
	void SetCustomLocation(VRCustomLocation@ location)
	{
		@CustomLocation = location;
	}
	
	void SetDuration(uint duration)
	{
		Duration = duration;
		Save();
	}
	
	void SetCombatMode(uint combatMode)
	{
		CombatMode = combatMode;
		Save();
	}
	
	void SetSpawn(uint spawn)
	{
		Spawn = spawn;
		Save();
	}
	
	bool IsDurationAvailable()
	{
		switch(Type)
		{
			case SIMULATION_TYPE_TEAM_DEATHMATCH:
				return true;
		}
		return false;
	}
	
	bool IsSpawnAvailable()
	{
		switch(Type)
		{
			case SIMULATION_TYPE_HUNT:
				return true;
		}
		return false;
	}
	
	string GetTypeInfo()
	{
		switch(Type)
		{
			case SIMULATION_TYPE_HUNT:
				return "Hunt";
			case SIMULATION_TYPE_TEAM_DEATHMATCH:
				return "Team deathmatch";
			case SIMULATION_TYPE_CUSTOM_LOCATION:
				return "Custom location";
		}
		return "NA";
	}
	
	string GetLocationInfo()
	{
		if(Type == SIMULATION_TYPE_CUSTOM_LOCATION)
		{
			if(!valid(CustomLocation))
				return "Not found";
			else
				return CustomLocation.Name;
				
		}
		switch(LocationPid)
		{
			case LOCATION_GreenForest1:
				return "Green forest labyrinth";
			case LOCATION_GreenForest2:
				return "Green forest lane";
		}
		return "Unknown (PID: " + LocationPid + ")";
	}
	
	string GetCombatModeInfo()
	{
		switch(CombatMode)
		{
			case COMBAT_MODE_REAL_TIME:
				return "Real time";
			case COMBAT_MODE_TURN_BASED:
				return "Turn-based";
		}
		return "NA";
	}
	
	string GetDurationInfo()
	{
		if(IsDurationAvailable())
			return Duration + " minutes";
		else
			return "NA";
	}
	
	string GetSpawnInfo()
	{
		switch(Spawn)
		{
			case 0:
				return "NA";
			case NPC_SPAWN_VR_GECKOS:
				return "Geckos";
			case NPC_SPAWN_VR_RAIDERS:
				return "Raiders";
		}
		return "Unknown: " + Spawn;
	}
		
	uint GetType()
	{
		return Type;
	}
		
	void Start()
	{
		Battle@ battle;
		uint locationPid;
		int respawnCount;
		switch(Type)
		{
			case SIMULATION_TYPE_HUNT:
				@battle = AddHunt(_Critter, CombatMode);
				locationPid = LocationPid;
				respawnCount = 0;
				break;
			case SIMULATION_TYPE_TEAM_DEATHMATCH:
				@battle = AddTeamDeathmatch(_Critter, CombatMode);
				locationPid = LocationPid;
				respawnCount = -1;
				break;
			case SIMULATION_TYPE_CUSTOM_LOCATION:
				@battle = AddCustomLocation(_Critter, CombatMode, CustomLocation.Name, CustomLocation.MultiLocation);
				locationPid = CustomLocation.LocationPid;
				respawnCount = -1;
				break;
		}
		
		if(!valid(battle))
		{
			_Critter.Say(SAY_NETMSG, "<error vr01> Cannot start simulation - battle creation failed");
			return;
		}

		battle.SetLocation(locationPid);
		battle.SetRespawnCount(respawnCount);
		if(IsDurationAvailable())
			battle.SetDuration(Duration);
		if(IsSpawnAvailable() && valid(battle.GetMap()))
			SpawnNpcs(Spawn, battle.GetMap());
			
		battle.SetEventWillStart(SimulationStarted());
		battle.SetEventFinished(SimulationFinished());
		battle.SetEventCancelled(SimulationFinished());
			
		battle.Join(_Critter);
	}
}

array<uint> InSimulation;

class SimulationStarted : BattleEvent
{
	void ProcessEvent(Battle& battle, Critter& critter)
	{
		Item@ hiddenContainer = FindHiddenContainer(critter.GetMap());
		
		if(!valid(hiddenContainer))
			return;
			
		ItemHolderWrapper itemHolder(hiddenContainer);
		
		Item@[] items;
		//holding current real world items
		critter.GetItems(SLOT_HAND1, items);
		itemHolder.PutItems(REAL_WORLD_ITEMS_PREFIX + HAND1_PREFIX + critter.Id, items);

		items.resize(0);
		critter.GetItems(SLOT_HAND2, items);
		itemHolder.PutItems(REAL_WORLD_ITEMS_PREFIX + HAND2_PREFIX + critter.Id, items);

		items.resize(0);
		critter.GetItems(SLOT_ARMOR, items);
		itemHolder.PutItems(REAL_WORLD_ITEMS_PREFIX + ARMOR_PREFIX + critter.Id, items);
		
		items.resize(0);
		critter.GetItems(SLOT_INV, items);
		itemHolder.PutItems(REAL_WORLD_ITEMS_PREFIX + INV_PREFIX + critter.Id, items);
		
		//getting his virtual reality items
		items = itemHolder.PopItems(VIRTUAL_REALITY_ITEMS_PREFIX + critter.Id);
		MoveItems(items, critter);
		
		InSimulation.insertLast(critter.Id);
	}
}

class SimulationFinished : BattleEvent
{
	void ProcessEvent(Battle& battle, Critter& critter)
	{
		if(InSimulation.find(critter.Id) < 0)
			return; //not in simulation
		
		Item@ hiddenContainer = FindHiddenContainer(critter.GetMap());
		//getting VR items
		Item@[] items;
		if(!valid(hiddenContainer))
		{
			critter.GetItems(-1, items);
			Log("Error occured while trying to save VR items. Deleting them all to prevent exploit.");
			DeleteItems(items);
			return;
		}
			
		ItemHolderWrapper itemHolder(hiddenContainer);
		
		//switching VR items with real world items
		critter.GetItems(-1, items);
		itemHolder.PutItems(VIRTUAL_REALITY_ITEMS_PREFIX + critter.Id, items);
		
		//getting RW items
		items = itemHolder.PopItems(REAL_WORLD_ITEMS_PREFIX + HAND1_PREFIX + critter.Id);
		if(!items.isEmpty())
		{
			MoveItems(items, critter);
			_CritMoveItem(critter, items[0], SLOT_HAND1);
		}
			
		items = itemHolder.PopItems(REAL_WORLD_ITEMS_PREFIX + HAND2_PREFIX + critter.Id);
		if(!items.isEmpty())
		{
			MoveItems(items, critter);
			_CritMoveItem(critter, items[0], SLOT_HAND2);
		}
			
		items = itemHolder.PopItems(REAL_WORLD_ITEMS_PREFIX + ARMOR_PREFIX + critter.Id);
		if(!items.isEmpty())
		{
			MoveItems(items, critter);
			_CritMoveItem(critter, items[0], SLOT_ARMOR);
		}
			
		items = itemHolder.PopItems(REAL_WORLD_ITEMS_PREFIX + INV_PREFIX + critter.Id);
		MoveItems(items, critter);
		
		InSimulation.removeAt(InSimulation.find(critter.Id));
	}
}
