/**
* Author: wladimiiir
*/

#include "_macros.fos"
#include "entire.fos"
#include "_npc_pids.fos"
#include "critter_action_basic.fos"
#include "critter_action_when.fos"
#include "critter_action_find.fos"
#include "critter_action_condition.fos"
#include "npc_spawn_h.fos"
#include "wrappers.fos"

#define	ENTIRE_TERMINAL			(100)
#define VIRTUAL_REALITY_TEAM	(1000)

const uint[] HEALING_DRUGS = {PID_SUPER_STIMPAK, PID_STIMPAK, PID_HEALING_POWDER};

/**
* Map script
*/
void InitMap(Map& map, bool firstTime)
{
	if(!firstTime)
		return;

	Entire[] entires;
	ParseEntires(map, entires, ENTIRE_TERMINAL);
	
	for (uint i = 0; i < entires.length(); i++)
	{		
		Item@ terminal = map.AddItem(entires[i].HexX, entires[i].HexY, PID_TERMINAL_SW, 1);
		if(valid(terminal))
			terminal.SetScript("TerminalInit");
	}
}

/**
* Initializing critter on first entire
*/
void InitCritter1(Critter& critter, bool)
{
	Action@ action = 
		LoopAction()
			.AddSubAction(FindCritters(FIND_LIFE | FIND_ONLY_PLAYERS)
				.If(IsSeen())
				.AddSubAction(Attack()))
			.AddSubAction(RepeatAtInterval(REAL_SECOND(5), REAL_SECOND(20))
				.If(NotAttacking())
				.AddSubAction(ChangeDirection(RANDOM_DIRECTION, true)))
			.AddSubAction(ProvideReinforcements(critter.Stat[ST_TEAM_ID])
				.If(NotAttacking()))
			.AddSubAction(ProvideReinforcements(VIRTUAL_REALITY_TEAM)
				.If(NotAttacking())
				.If(IsSeen()))
			.AddSubAction(WhenAttackEnds()
				.AddSubAction(SetHome()))
			.AddSubAction(WhenAttacked()
				.AddSubAction(CallReinforcements(critter.Stat[ST_TEAM_ID]))
				.AddSubAction(CallReinforcements(VIRTUAL_REALITY_TEAM)))
			.AddSubAction(WhenPlaneRun()
				.If(IsInjured(70, true))
				.AddSubAction(UseDrug(HEALING_DRUGS)))
	;
	actionManager.Start(critter, action);
}

/**
* Initializing critter on second entire
*/
void InitCritter2(Critter& critter, bool)
{
	InitCritter1(critter, false);
}

/**
* Initializing terminal
*/
void TerminalInit(Item& item, bool)
{
	item.SetEvent(ITEM_EVENT_SKILL, "TerminalSkill");
}

bool TerminalSkill(Item& door, Critter& cr, int skill)
{
	if(skill != SKILL_PICK_ON_GROUND)
		return false;
	
	//start dialog
	return true;
}

/**
* Test scripts
*
* Green forest	=	300
*/
import Location@ CreateLocationForCritter( Critter& player, int locPid, int worldX, int worldY, int delta, int varNum ) from "location";

void CreateVirtualReality(Critter& critter, int locationPid, int combatMode, int spawnId)
{
	uint locationId = CreateLocation(locationPid, critter.WorldX, critter.WorldY, null);
	if(locationId == 0)
		return;
		
	Location@ location = GetLocation(locationId);
	if(!valid(location))
		return;
	
	Map@ map = location.GetMapByIndex(0);
	if(combatMode == COMBAT_MODE_TURN_BASED)
		map.SetTurnBasedAvailability(true);
		
	map.SetScript("InitMap");
	Critter@[] critters = SpawnNpcs(spawnId, map);
	Log("Added "+critters.length() +" critters.");
	
	critter.TransitToMap(map.Id, 0);
}
